import org.springframework.boot.gradle.plugin.SpringBootPlugin

plugins {
    id "java"
    id "jacoco"
    id "org.sonarqube" version "4.0.0.2929"
    id "info.solidsoft.pitest" version "1.9.11"
    id "org.springframework.boot" version "3.1.2"
    id "org.jsonschema2dataclass" version "6.0.0"
}

group = "uk.gov.justice.laa.crime"

jacoco {
    toolVersion = "0.8.8"
}

def versions = [
        okhttpVersion                      : "4.9.3",
        pitest                             : "1.4.10",
        amazonSQSVersion                   : "2.1.1",
        springFrameworkCloudVersion        : "3.0.1",
        crimeCommonsVersion                : "2.5.0",
        mockitoInlineVersion               : "5.2.0",
        wiremockVersion                    : "2.35.0"
]

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}


dependencies {

    implementation platform("io.sentry:sentry-bom:6.15.0")
    implementation platform(SpringBootPlugin.BOM_COORDINATES)
    annotationProcessor platform(SpringBootPlugin.BOM_COORDINATES)

    implementation "io.sentry:sentry-logback"
    implementation "io.sentry:sentry-spring-boot-starter"

    implementation "io.awspring.cloud:spring-cloud-aws-starter-sqs:$versions.springFrameworkCloudVersion"

    implementation "org.springframework:spring-jms"
    implementation "org.springframework.boot:spring-boot-starter-web"
    implementation "org.springframework.boot:spring-boot-starter-actuator"
    implementation "org.springframework.boot:spring-boot-starter-webflux"
    implementation "org.springframework.boot:spring-boot-starter-validation"
    implementation "org.springframework.boot:spring-boot-starter-oauth2-resource-server"
    implementation "org.springframework.boot:spring-boot-starter-oauth2-client"
    implementation "org.springframework.boot:spring-boot-starter-data-jpa"

    implementation "org.postgresql:postgresql"
    implementation "com.google.code.gson:gson"
    implementation "org.liquibase:liquibase-core"
    implementation "org.apache.commons:commons-lang3"
    implementation "io.micrometer:micrometer-registry-prometheus"
    implementation "org.springdoc:springdoc-openapi-starter-webmvc-ui:2.0.4"
    implementation "com.amazonaws:amazon-sqs-java-messaging-lib:$versions.amazonSQSVersion"
    implementation 'software.amazon.awssdk:sts:2.20.69'

    compileOnly "org.projectlombok:lombok"
    annotationProcessor "org.projectlombok:lombok"
    annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"

    implementation "uk.gov.justice.service.laa-crime:crime-commons-spring-boot-starter-rest-client:$versions.crimeCommonsVersion"
    
    testImplementation "com.h2database:h2"
    testImplementation "org.mockito:mockito-inline:$versions.mockitoInlineVersion"
    testImplementation "org.springframework.boot:spring-boot-starter-test"
    testImplementation "org.springframework.security:spring-security-test"
    testImplementation "org.pitest:pitest:$versions.pitest"
    testImplementation "com.github.tomakehurst:wiremock-jre8-standalone:$versions.wiremockVersion"



    testImplementation 'org.testcontainers:testcontainers:1.18.3'
    testImplementation 'org.testcontainers:localstack:1.18.3'
    //testImplementation 'org.testcontainers:junit-jupiter:1.18.3'
    testImplementation 'cloud.localstack:localstack-utils:0.2.23'
    testImplementation group: 'com.github.tomakehurst', name: 'wiremock', version: '2.27.2', ext: 'pom'

    implementation group: 'com.amazonaws', name: 'aws-java-sdk', version: '1.12.264'
    implementation group: 'com.amazonaws', name: 'aws-java-sdk-sqs', version: '1.12.264'
    testImplementation "org.testcontainers:junit-jupiter:1.19.0"



}

configurations {
    configureEach {
        exclude group: 'junit', module: 'junit'
    }
}

test {
    useJUnitPlatform()
    dependsOn "cleanTest"
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    onlyIf = { true }
    reports {
        xml.required = true
        html.required = true
    }
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it,
                    exclude: ["**/entity/**",
                              "**/dto/**",
                              "**/config/**"])
        }))
    }
}

pitest {
    targetClasses = ["uk.gov.justice.laa.crime.crowncourt.*","uk.gov.justice.laa.crime.crowncourt.controller.*","uk.gov.justice.laa.crime.crowncourt.validation.*"]
    excludedClasses = ["uk.gov.justice.laa.crime.crowncourt.dto.*", "uk.gov.justice.laa.crime.crowncourt.mapper.*", "uk.gov.justice.laa.crime.crowncourt.CrownCourtProceedingApplication"]
    threads = 5
    outputFormats = ["XML", "HTML"]
    timestampedReports = false
    mutationThreshold = 60
}

tasks["sonar"].dependsOn test

sonar {
    properties {
        property "sonar.exclusions", "**/entity/**.java , **/model/**.java , **/dto/**.java , **/config/**.java, **/jms/**.java,**/exception/**.java,**/handler/**.java"
        property "sonar.coverage.exclusions", "**/CrownCourtProceedingApplication.java"
        property "sonar.coverage.jacoco.xmlReportPaths", "${project.buildDir}/reports/jacoco/test/jacocoTestReport.xml"
        property "sonar.projectKey", "ministryofjustice_laa-crown-court-proceeding"
        property "sonar.organization", "ministryofjustice"
        property "sonar.host.url", "https://sonarcloud.io"
        property "sonar.projectName", "Crime Apps :: crown-court-proceeding"
        property "sonar.pitest.mode", "reuseReport"
        property "sonar.pitest.reportsDirectory", "build/reports/pitest"
    }
}

jsonSchema2Pojo {
    executions {
        main {
            methods.builders = true
            methods.buildersDynamic = true
            methods.annotateJsr303Jakarta = true
            fields.floatUseBigDecimal = true
            dateTime.dateTimeType = "java.time.LocalDateTime"
            klass.targetPackage = "uk.gov.justice.laa.crime.crowncourt.model"
            io.source.setFrom files("${project.rootDir}/src/main/resources/schemas")
        }
    }
}

