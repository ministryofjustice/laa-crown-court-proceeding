plugins {
    id "java"
    id "jacoco"
    id "org.sonarqube" version "7.0.1.6134"
    id "com.diffplug.spotless" version "8.0.0"
    id "info.solidsoft.pitest" version "1.15.0"
    id "org.springframework.boot" version "3.5.7"
    id "io.spring.dependency-management" version "1.1.7"
}

group = "uk.gov.justice.laa.crime"

jacoco {
    toolVersion = "0.8.13"
}

def versions = [
        sentry                  : "8.26.0",
        sqsMessaging            : "2.1.4",
        springCloud             : "3.4.1",
        crimeCommonsClasses     : "4.10.0",
        commonsModSchemas       : "1.36.0",
        springDoc               : "2.8.14",
        awsSTS                  : "2.39.2",
        awsJavaSDK              : "1.12.793",
        testcontainers          : "1.21.3",
        resilience4j            : "2.3.0",
        wiremock                : "4.0.8",
        notifyClient            : "5.2.1-RELEASE"
]

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
    testSqsIntegrationImplementation.extendsFrom testImplementation
}

sourceSets {
    testSqsIntegration {
        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output
    }
}

repositories {
    mavenCentral()
}

dependencies {

    // ---- Monitoring & Logging ----
    implementation platform("io.sentry:sentry-bom:$versions.sentry")
    implementation "io.sentry:sentry-logback"
    implementation 'io.micrometer:micrometer-tracing'
    implementation "io.micrometer:micrometer-registry-prometheus"
    implementation 'io.micrometer:micrometer-tracing-bridge-brave'
    implementation "io.sentry:sentry-spring-boot-starter-jakarta"

    // ---- Core Application ----
    implementation "uk.gov.justice.service.laa-crime:crime-commons-classes:$versions.crimeCommonsClasses"
    implementation "uk.gov.justice.service.laa-crime:crime-commons-mod-schemas:$versions.commonsModSchemas"

    implementation "org.springframework.boot:spring-boot-starter-web"
    implementation "org.springframework.boot:spring-boot-starter-webflux"
    implementation "org.springframework.boot:spring-boot-starter-actuator"
    implementation "org.springframework.boot:spring-boot-starter-data-jpa"
    implementation "org.springframework.boot:spring-boot-starter-validation"
    implementation "org.springframework.boot:spring-boot-starter-oauth2-client"
    implementation "org.springframework.boot:spring-boot-starter-oauth2-resource-server"

    implementation "org.postgresql:postgresql"
    implementation "org.liquibase:liquibase-core"
    implementation "org.springdoc:springdoc-openapi-starter-webmvc-ui:$versions.springDoc"
    implementation "uk.gov.service.notify:notifications-java-client:$versions.notifyClient"

    // ---- Queue Dependencies ----
    implementation "com.google.code.gson:gson"
    implementation "org.springframework:spring-jms"
    implementation "software.amazon.awssdk:sts:$versions.awsSTS"
    implementation "com.amazonaws:amazon-sqs-java-messaging-lib:$versions.sqsMessaging"
    implementation "io.awspring.cloud:spring-cloud-aws-starter-sqs:$versions.springCloud"

    // ---- Resilience4j ----
    implementation "io.github.resilience4j:resilience4j-reactor:$versions.resilience4j"
    implementation "io.github.resilience4j:resilience4j-spring-boot3:$versions.resilience4j"

    // ---- Lombok ----
    compileOnly "org.projectlombok:lombok"
    annotationProcessor "org.projectlombok:lombok"
    annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"

    // ---- Testing ----
    testImplementation "com.h2database:h2"
    testImplementation "org.springframework.boot:spring-boot-starter-test"
    testImplementation "org.wiremock.integrations:wiremock-spring-boot:$versions.wiremock"

    testCompileOnly "org.projectlombok:lombok"
    testAnnotationProcessor "org.projectlombok:lombok"

    // ---- Integration Testing ----
    testSqsIntegrationImplementation "com.amazonaws:aws-java-sdk-sqs:$versions.awsJavaSDK"
    testSqsIntegrationImplementation "org.testcontainers:localstack:$versions.testcontainers"
    testSqsIntegrationImplementation "org.testcontainers:junit-jupiter:$versions.testcontainers"
}

spotless {
    java {
        target 'src/*/java/**/*.java'
        targetExclude '**/generated/**', '**/build/**'

        formatAnnotations()
        palantirJavaFormat()
        importOrder('', 'java', 'javax', 'org', 'com')

        endWithNewline()
        trimTrailingWhitespace()
        removeUnusedImports()
        forbidWildcardImports()

        // Custom rule: Disallow JUnit assertions
        custom("Refuse JUnit assertions", { text ->
            if (text =~ /\bassert(?:Equals|True|False|Null|NotNull|Same|NotSame|ArrayEquals)\b/) {
                throw new IllegalStateException(
                        "Use AssertJ (assertThat(...)) instead of JUnit assertions."
                )
            }
            text
        } as Closure<String>)
    }
}

test {
    useJUnitPlatform()
    dependsOn "cleanTest"
    finalizedBy jacocoTestReport
}

tasks.register('testSqsIntegration', Test) {
    useJUnitPlatform()
    classpath = sourceSets.testSqsIntegration.runtimeClasspath
    testClassesDirs = sourceSets.testSqsIntegration.output.classesDirs
}

check.dependsOn testSqsIntegration

jacocoTestReport {
    onlyIf = { true }
    reports {
        xml.required = true
        html.required = true
    }
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it,
                    exclude: ["**/entity/**",
                              "**/dto/**",
                              "**/config/**"])
        }))
    }
}

pitest {
    targetClasses = ["uk.gov.justice.laa.crime.crowncourt.*",
                     "uk.gov.justice.laa.crime.crowncourt.controller.*",
                     "uk.gov.justice.laa.crime.crowncourt.validation.*"
    ]
    excludedClasses = ["uk.gov.justice.laa.crime.crowncourt.dto.*",
                       "uk.gov.justice.laa.crime.crowncourt.mapper.*",
                       "uk.gov.justice.laa.crime.crowncourt.CrownCourtProceedingApplication"
    ]
    threads = 5
    mutationThreshold = 60
    timestampedReports = false
    outputFormats = ["XML", "HTML"]
}

def isCiBuild = System.getenv("SONAR_TOKEN") != null

if (isCiBuild) {
    tasks.named("sonar") {
        dependsOn "test"
    }
}

sonar {
    skipProject = !isCiBuild
    properties {
        property "sonar.exclusions", "**/entity/**.java , **/model/**.java , **/dto/**.java , **/config/**.java, **/jms/**.java,**/exception/**.java,**/handler/**.java"
        property "sonar.coverage.exclusions", "**/CrownCourtProceedingApplication.java"
        property "sonar.coverage.jacoco.xmlReportPaths", "${project.buildDir}/reports/jacoco/test/jacocoTestReport.xml"
        property "sonar.projectKey", "ministryofjustice_laa-crown-court-proceeding"
        property "sonar.organization", "ministryofjustice"
        property "sonar.host.url", "https://sonarcloud.io"
        property "sonar.projectName", "Crime Apps :: crown-court-proceeding"
        property "sonar.pitest.mode", "reuseReport"
        property "sonar.pitest.reportsDirectory", "build/reports/pitest"
    }
}

