import org.springframework.boot.gradle.plugin.SpringBootPlugin

plugins {
    id "java"
    id "jacoco"
    id "org.springframework.boot" version "3.0.5"
    id "org.sonarqube" version "4.0.0.2929"
    id "info.solidsoft.pitest" version "1.9.11"
    id "org.jsonschema2dataclass" version "6.0.0"
    id 'io.spring.dependency-management' version '1.1.2'
}

group = "uk.gov.justice.laa.crime"

jacoco {
    toolVersion = "0.8.8"
}

def versions = [
        okhttpVersion                      : "4.9.3",
        mockwebserverVersion               : "4.10.0",
        pitest                             : "1.4.10",
        amazonSQSVersion                   : "2.1.1",
        xRayVersion                        : "2.11.2",
        springFrameworkCloudVersion        : "2.2.6.RELEASE",
]

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}


dependencies {

    implementation platform("io.sentry:sentry-bom:6.15.0")
    implementation platform(SpringBootPlugin.BOM_COORDINATES)
    annotationProcessor platform(SpringBootPlugin.BOM_COORDINATES)

    implementation "io.sentry:sentry-logback"
    implementation "io.sentry:sentry-spring-boot-starter"

    //implementation "org.springframework.cloud:spring-cloud-starter-aws:$versions.springFrameworkCloudVersion"
    //implementation "org.springframework.cloud:spring-cloud-starter-aws-messaging:$versions.springFrameworkCloudVersion"
    //implementation "org.springframework.cloud:spring-cloud-aws-autoconfigure:$versions.springFrameworkCloudVersion"
    //implementation group: 'org.springframework', name: 'spring-messaging', version: '5.3.25'

    implementation 'io.awspring.cloud:spring-cloud-aws-sqs:3.0.1'

    implementation "org.springframework:spring-jms"
    implementation "org.springframework.boot:spring-boot-starter-web"
    implementation "org.springframework.boot:spring-boot-starter-actuator"
    implementation "org.springframework.boot:spring-boot-starter-webflux"
    implementation "org.springframework.boot:spring-boot-starter-validation"
    implementation "org.springframework.boot:spring-boot-starter-oauth2-resource-server"
    implementation "org.springframework.boot:spring-boot-starter-oauth2-client"
    implementation "org.springframework.boot:spring-boot-starter-data-jpa"

    implementation "org.postgresql:postgresql"
    implementation "com.google.code.gson:gson"
    implementation "org.liquibase:liquibase-core"
    implementation "org.apache.commons:commons-lang3"
    implementation "io.micrometer:micrometer-registry-prometheus"
    implementation "org.springdoc:springdoc-openapi-starter-webmvc-ui:2.0.4"
    //implementation "com.amazonaws:amazon-sqs-java-messaging-lib:$versions.amazonSQSVersion"

    implementation group: 'com.amazonaws', name: 'aws-xray-recorder-sdk-sql', version: versions.xRayVersion
    implementation group: 'com.amazonaws', name: 'aws-xray-recorder-sdk-slf4j', version: versions.xRayVersion
    //implementation group: 'com.amazonaws', name: 'amazon-sqs-java-messaging-lib', version: versions.amazonSQSVersion
    implementation group: 'com.amazonaws', name: 'aws-xray-recorder-sdk-spring', version: versions.xRayVersion

    compileOnly "org.projectlombok:lombok"
    annotationProcessor "org.projectlombok:lombok"
    annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"

    implementation "uk.gov.justice.service.laa-crime:crime-commons-spring-boot-starter-rest-client:2.1.0"
    
    testImplementation "com.h2database:h2"
    testImplementation "org.mockito:mockito-inline"
    testImplementation "org.springframework.boot:spring-boot-starter-test"
    testImplementation "org.springframework.security:spring-security-test"
    testImplementation "org.pitest:pitest:$versions.pitest"
    testImplementation "com.squareup.okhttp3:okhttp:$versions.okhttpVersion"
    testImplementation "com.squareup.okhttp3:mockwebserver:$versions.mockwebserverVersion"
}

tasks.named("test").configure {
    it.useJUnitPlatform()
    it.dependsOn "cleanTest"
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    onlyIf = { true }
    reports {
        xml.required = true
        html.required = true
    }
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it,
                    exclude: ["**/entity/**",
                              "**/dto/**",
                              "**/config/**"])
        }))
    }
}

pitest {
    targetClasses = ["uk.gov.justice.laa.crime.crowncourt.*","uk.gov.justice.laa.crime.crowncourt.controller.*","uk.gov.justice.laa.crime.crowncourt.validation.*"]
    excludedClasses = ["uk.gov.justice.laa.crime.crowncourt.dto.*", "uk.gov.justice.laa.crime.crowncourt.mapper.*", "uk.gov.justice.laa.crime.crowncourt.CrownCourtProceedingApplication"]
    threads = 5
    outputFormats = ["XML", "HTML"]
    timestampedReports = false
    mutationThreshold = 60
}

tasks["sonar"].dependsOn test

sonar {
    properties {
        property "sonar.exclusions", "**/entity/**.java , **/model/**.java , **/dto/**.java , **/config/**.java, **/jms/**.java,**/exception/**.java,**/handler/**.java"
        property "sonar.coverage.exclusions", "**/CrownCourtProceedingApplication.java"
        property "sonar.coverage.jacoco.xmlReportPaths", "${project.buildDir}/reports/jacoco/test/jacocoTestReport.xml"
        property "sonar.projectKey", "ministryofjustice_laa-crown-court-proceeding"
        property "sonar.organization", "ministryofjustice"
        property "sonar.host.url", "https://sonarcloud.io"
        property "sonar.projectName", "Crime Apps :: crown-court-proceeding"
        property "sonar.pitest.mode", "reuseReport"
        property "sonar.pitest.reportsDirectory", "build/reports/pitest"
    }
}

jsonSchema2Pojo {
    executions {
        main {
            methods.builders = true
            methods.buildersDynamic = true
            methods.annotateJsr303Jakarta = true
            fields.floatUseBigDecimal = true
            dateTime.dateTimeType = "java.time.LocalDateTime"
            klass.targetPackage = "uk.gov.justice.laa.crime.crowncourt.model"
            io.source.setFrom files("${project.rootDir}/src/main/resources/schemas")
        }
    }
}

