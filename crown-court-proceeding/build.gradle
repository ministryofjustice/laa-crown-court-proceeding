plugins {
    id "java"
    id "jacoco"
    id "org.sonarqube" version "6.1.0.5360"
    id "info.solidsoft.pitest" version "1.15.0"
    id "org.springframework.boot" version "3.4.5"
    id "io.spring.dependency-management" version "1.1.6"
}

group = "uk.gov.justice.laa.crime"

jacoco {
    toolVersion = "0.8.8"
}

def versions = [
        sentry                           : "7.12.1",
        okhttpVersion                    : "4.9.3",
        pitest                           : "1.4.10",
        amazonSQSVersion                 : "2.1.1",
        springFrameworkCloudVersion      : "3.0.1",
        crimeCommonsClasses              : "4.7.1",
        commonsModSchemas                : "1.34.0",
        mockitoInlineVersion             : "5.2.0",
        springDocWebMVCVersion           : "2.1.0",
        awsSdkStsVersion                 : "2.20.69",
        awsJavaSdkVersion                : "1.12.783",
        localstackUtilsVersion           : "0.2.23",
        testcontainersLocalstackVersion  : "1.18.3",
        testcontainersJunitjupiterVersion: "1.19.0",
        notifyClientVersion              : "5.1.0-RELEASE",
        resilience4jVersion              : "2.2.0",
        wiremock                         : "3.10.0"
]

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

sourceSets {
    testSqsIntegration {
        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output
    }
}


configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
    testSqsIntegrationImplementation.extendsFrom implementation
}

repositories {
    mavenCentral()
}

dependencies {

    implementation platform("io.sentry:sentry-bom:$versions.sentry")
    implementation "io.sentry:sentry-spring-boot-starter-jakarta"
    implementation "io.sentry:sentry-logback"
    
    implementation "uk.gov.justice.service.laa-crime:crime-commons-classes:$versions.crimeCommonsClasses"
    implementation "uk.gov.justice.service.laa-crime:crime-commons-mod-schemas:$versions.commonsModSchemas"
    
    implementation "org.springframework.boot:spring-boot-starter-actuator"
    implementation "org.springframework.boot:spring-boot-starter-web"
    implementation "org.springframework.boot:spring-boot-starter-webflux"
    implementation "org.springframework.boot:spring-boot-starter-validation"
    implementation "org.springframework.boot:spring-boot-starter-oauth2-client"
    implementation "org.springframework.boot:spring-boot-starter-oauth2-resource-server"
    implementation "org.springframework.boot:spring-boot-starter-data-jpa"

    implementation "org.springframework:spring-jms"
    implementation "software.amazon.awssdk:sts:$versions.awsSdkStsVersion"
    implementation "com.amazonaws:amazon-sqs-java-messaging-lib:$versions.amazonSQSVersion"
    implementation "io.awspring.cloud:spring-cloud-aws-starter-sqs:$versions.springFrameworkCloudVersion"

    implementation "org.springdoc:springdoc-openapi-starter-webmvc-ui:$versions.springDocWebMVCVersion"

    implementation "io.github.resilience4j:resilience4j-reactor:$versions.resilience4jVersion"
    implementation "io.github.resilience4j:resilience4j-spring-boot3:$versions.resilience4jVersion"

    implementation "org.postgresql:postgresql"
    implementation "org.liquibase:liquibase-core"

    implementation "com.google.code.gson:gson"

    implementation 'io.micrometer:micrometer-tracing'
    implementation "io.micrometer:micrometer-registry-prometheus"
    implementation 'io.micrometer:micrometer-tracing-bridge-brave'

    implementation "uk.gov.service.notify:notifications-java-client:$versions.notifyClientVersion"

    compileOnly "org.projectlombok:lombok"
    annotationProcessor "org.projectlombok:lombok"
    annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"

    testImplementation "com.h2database:h2"
    testImplementation "org.pitest:pitest:$versions.pitest"
    testImplementation "org.springframework.security:spring-security-test"
    testImplementation "org.springframework.boot:spring-boot-starter-test"
    testImplementation "org.wiremock.integrations:wiremock-spring-boot:${versions.wiremock}"

    testCompileOnly "org.projectlombok:lombok"
    testAnnotationProcessor "org.projectlombok:lombok"
    //dependencies for ProsecutionListenerTest only
    testSqsIntegrationImplementation "com.h2database:h2"
    testSqsIntegrationImplementation "org.springframework.boot:spring-boot-starter-test"
    testSqsIntegrationImplementation "com.amazonaws:aws-java-sdk:$versions.awsJavaSdkVersion"
    testSqsIntegrationImplementation "com.amazonaws:aws-java-sdk-sqs:$versions.awsJavaSdkVersion"
    testSqsIntegrationImplementation "cloud.localstack:localstack-utils:$versions.localstackUtilsVersion"
    testSqsIntegrationImplementation "org.wiremock.integrations:wiremock-spring-boot:${versions.wiremock}"
    testSqsIntegrationImplementation "org.testcontainers:localstack:$versions.testcontainersLocalstackVersion"
    testSqsIntegrationImplementation "org.testcontainers:junit-jupiter:$versions.testcontainersJunitjupiterVersion"
}

configurations {
    testImplementation {
        exclude group: 'junit', module: 'junit'
    }
}

test {
    useJUnitPlatform()
    dependsOn "cleanTest"
    finalizedBy jacocoTestReport
}

task testSqsIntegration(type: Test) {
    useJUnitPlatform()
    testClassesDirs = sourceSets.testSqsIntegration.output.classesDirs
    classpath = sourceSets.testSqsIntegration.runtimeClasspath
}

check.dependsOn testSqsIntegration

jacocoTestReport {
    onlyIf = { true }
    reports {
        xml.required = true
        html.required = true
    }
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it,
                    exclude: ["**/entity/**",
                              "**/dto/**",
                              "**/config/**"])
        }))
    }
}

pitest {
    targetClasses = ["uk.gov.justice.laa.crime.crowncourt.*",
                     "uk.gov.justice.laa.crime.crowncourt.controller.*",
                     "uk.gov.justice.laa.crime.crowncourt.validation.*"
    ]
    excludedClasses = ["uk.gov.justice.laa.crime.crowncourt.dto.*",
                       "uk.gov.justice.laa.crime.crowncourt.mapper.*",
                       "uk.gov.justice.laa.crime.crowncourt.CrownCourtProceedingApplication"
    ]
    threads = 5
    mutationThreshold = 60
    timestampedReports = false
    outputFormats = ["XML", "HTML"]
}

def isCiBuild = System.getenv("SONAR_TOKEN") != null

if (isCiBuild) {
    tasks.named("sonar") {
        dependsOn "test"
    }
}

sonar {
    skipProject = !isCiBuild
    properties {
        property "sonar.exclusions", "**/entity/**.java , **/model/**.java , **/dto/**.java , **/config/**.java, **/jms/**.java,**/exception/**.java,**/handler/**.java"
        property "sonar.coverage.exclusions", "**/CrownCourtProceedingApplication.java"
        property "sonar.coverage.jacoco.xmlReportPaths", "${project.buildDir}/reports/jacoco/test/jacocoTestReport.xml"
        property "sonar.projectKey", "ministryofjustice_laa-crown-court-proceeding"
        property "sonar.organization", "ministryofjustice"
        property "sonar.host.url", "https://sonarcloud.io"
        property "sonar.projectName", "Crime Apps :: crown-court-proceeding"
        property "sonar.pitest.mode", "reuseReport"
        property "sonar.pitest.reportsDirectory", "build/reports/pitest"
    }
}

