plugins {
    id 'java'
    id 'jacoco'
    id 'org.springframework.boot' version '2.7.5'
    id 'io.spring.dependency-management' version '1.1.0'
    id "org.sonarqube" version "3.5.0.2730"
    id 'info.solidsoft.pitest' version '1.9.11'
    id 'org.jsonschema2dataclass' version '4.5.0'
}

group = 'uk.gov.justice.laa.crime'

jacoco {
    toolVersion = "0.8.8"
}

def versions = [
        sentryVersion                      : '6.6.0',
        springdocVersion                   : '1.6.14',
        springauthserverVersion            : '0.2.2',
        sonarPitest                        : '0.5',
        mockitoVersion                     : '3.3.3',
        junitVersion                       : '5.9.1',
        harmcrestVersion                   : '1.3',
        testngVersion                      : '6.10',
        festVersion                        : '1.4',
        reactorTestVersion                 : '3.4.11',
        okhttpVersion                      : '4.9.3',
        mockwebserverVersion               : '4.9.3',
        gradlePitest                       : '1.7.4',
        pitest                             : '1.4.10',
        commonsCollections4Version         : '4.4',
        commonsLang3Version                : '3.10',
        lombokVersion                      : '1.18.24',
        amazonSQSVersion                   : '1.0.8'
]
dependencyManagement {
    dependencies {
        dependencySet(group: 'io.sentry', version: versions.sentryVersion) {
            entry 'sentry-spring-boot-starter'
            entry 'sentry-logback'
        }
    }
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter'
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-web'
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-actuator'
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-webflux'
    implementation group: 'org.springframework.boot', name: 'spring-boot-autoconfigure'
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-validation'
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-security'
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-oauth2-resource-server'
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-oauth2-client'
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-test'

    implementation group: 'org.springframework.security', name: 'spring-security-oauth2-jose'
    implementation group: 'org.springframework.security', name: 'spring-security-cas'
    implementation group: 'org.springframework.security', name: 'spring-security-oauth2-authorization-server', version: versions.springauthserverVersion
    implementation group: 'org.springframework', name: 'spring-jms'
    implementation group: 'io.projectreactor.netty', name: 'reactor-netty'

    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation "org.liquibase:liquibase-core"
    implementation "org.springframework.boot:spring-boot-starter-actuator"
    implementation "org.springdoc:springdoc-openapi-ui:$versions.springdocVersion"

    implementation group: 'io.sentry', name: 'sentry-spring-boot-starter'
    implementation group: 'io.sentry', name: 'sentry-logback'

    implementation 'org.postgresql:postgresql'
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-data-jpa'

    implementation group: 'org.pitest', name: 'pitest', version: versions.pitest

    implementation group: 'org.apache.commons', name: 'commons-collections4', version: versions.commonsCollections4Version

    implementation group: 'com.google.code.gson', name: 'gson', version: versions.gsonVersion
    implementation group: 'org.apache.commons', name: 'commons-lang3', version: versions.commonsLang3Version

    compileOnly group: 'org.projectlombok', name: 'lombok', version: versions.lombokVersion
    annotationProcessor group: 'org.projectlombok', name: 'lombok', version: versions.lombokVersion

    // AWS
    implementation group: 'com.amazonaws', name: 'amazon-sqs-java-messaging-lib', version: versions.amazonSQSVersion

    developmentOnly 'org.springframework.boot:spring-boot-devtools'

    runtimeOnly "org.postgresql:postgresql"

    annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"

    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: versions.junitVersion
    testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: versions.junitVersion

    testImplementation "com.h2database:h2"
    testImplementation "org.springframework.boot:spring-boot-starter-test"
    testImplementation "org.springframework.security:spring-security-test"
    testImplementation group: 'org.mockito', name: 'mockito-inline', version: versions.mockitoVersion
    testImplementation group: 'org.hamcrest', name: 'hamcrest-all', version: versions.harmcrestVersion
    testImplementation group: 'org.testng', name: 'testng', version: versions.testngVersion
    testImplementation group: 'org.easytesting', name: 'fest-assert', version: versions.festVersion
    testImplementation group: 'io.projectreactor', name: 'reactor-test', version: versions.reactorTestVersion
    testImplementation group: 'com.squareup.okhttp3', name: 'okhttp', version: versions.okhttpVersion
    testImplementation group: 'com.squareup.okhttp3', name: 'mockwebserver', version: versions.mockwebserverVersion
}

tasks.named('test').configure {
    it.useJUnitPlatform()
    it.dependsOn 'cleanTest'
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    onlyIf = { true }
    reports {
        xml.required = true
        html.required = true
    }
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it,
                    exclude: ['**/entity/**',
                              '**/dto/**',
                              '**/config/**'])
        }))
    }
}

pitest {
    targetClasses = ['uk.gov.justice.laa.crime.crowncourt.*','uk.gov.justice.laa.crime.crowncourt.controller.*','uk.gov.justice.laa.crime.crowncourt.validation.*']
    excludedClasses = ['uk.gov.justice.laa.crime.crowncourt.dto.*', 'uk.gov.justice.laa.crime.crowncourt.mapper.*', 'uk.gov.justice.laa.crime.crowncourt.CrownCourtProceedingApplication']
    threads = 5
    outputFormats = ['XML', 'HTML']
    timestampedReports = false
    mutationThreshold = 60
}

tasks['sonarqube'].dependsOn test

sonarqube {
    properties {
        property "sonar.exclusions", "**/entity/**.java , **/model/**.java , **/dto/**.java , **/config/**.java, **/jms/**.java,**/exception/**.java,**/handler/**.java"
        property "sonar.coverage.exclusions", "**/CrownCourtProceedingApplication.java"
        property "sonar.coverage.jacoco.xmlReportPaths", "${project.buildDir}/reports/jacoco/test/jacocoTestReport.xml"
        property "sonar.projectKey", "ministryofjustice_laa-crown-court-proceeding"
        property "sonar.organization", "ministryofjustice"
        property "sonar.host.url", "https://sonarcloud.io"
        property "sonar.projectName", "Crime Apps :: crown-court-proceeding"
        property "sonar.pitest.mode", "reuseReport"
        property "sonar.pitest.reportsDirectory", "build/reports/pitest"
    }
}

jsonSchema2Pojo {
    source.setFrom files("${project.rootDir}/src/main/resources/schemas")
    targetPackage = 'uk.gov.justice.laa.crime.crowncourt.model'
    generateBuilders = true
    useInnerClassBuilders = true
    includeJsr303Annotations = true
    useBigDecimals = true
    includeDynamicBuilders = true
    dateTimeType = "java.time.LocalDateTime"
}

